# Tail Recursion Modulo Pattern Matching with Trampolines

## Background

*mcfunction*s also have the concept of tail recursion.
For instance,

- `f.mcfunction`
```mcfunction
say f
function f
```

is a tail-recursive and uses a constant queue space, while

- `g.mcfunction`
```mcfunction
function g
say g
```

is not.

## Pattern Matching Interferes Tail Recursion

Consider the following pseudo bottle-counting function:

```lean
def count_bottles n :=
  match n with
  | 0 => print "no more bottles"
  | 1 => print "1 bottle"; count_bottles 0
  | _ => print "{n} bottles"; count_bottles (n * 0.9999999999999999)
```

`count_bottles` translates naÃ¯vely into the following mcfunctions:

- `count_bottles.mcfunction`
```mcfunction
# match n with
data modify storage _ matched set value false
# | 0 =>
execute if data storage _ {n: 0} run function count_bottles-0
# | 1 =>
execute if data storage _ {matched: false} if data storage _ {n: 1} run function count_bottles-1
# | _ =>
execute if data storage _ {matched: false} run function count_bottles-2
```
- `count_bottles-0.mcfunction`
```mcfunction
# print "no more bottles"
tellraw @s "no more bottles"

# prevent fall-through
data modify storage _ matched set value true
```
- `count_bottles-1.mcfunction`
```mcfunction
# print "1 bottle"
tellraw @s "1 bottle"

# count_bottles 0
data modify storage _ n set value 0
function count_bottles

# prevent fall-through
data modify storage _ matched set value true
```
- `count_bottles-2.mcfunction`
```mcfunction
# print "{n} bottles"
tellraw @s [{"nbt": "n", "storage": "_"}, " bottles"]

# count_bottles (n * 0.9999999999999999)
execute store result storage _ n int 0.9999999999999999 run data get storage _ n
function count_bottles
```

This transformation is obviously not tail-recursive (`function count_bottles` appears in non-tail recursive positions!) and uses a linear queue space for `n`.

## Trampolines to the Rescue

The cause of the problem is a recursive call in the middle of a pattern matching.
So, let's delay the recursive calls!
To achieve this, we will use the specialized form of the well-known technique of *trampolines*.

- `count_bottles.mcfunction`
```mcfunction
# match n with
data modify storage _ matched set value false
# | 0 =>
execute if data storage _ {n: 0} run function count_bottles-0
# | 1 =>
execute if data storage _ {matched: false} if data storage _ {n: 1} run function count_bottles-1
# | _ =>
execute if data storage _ {matched: false} run function count_bottles-2

# if not done, then call count_bottles
execute if data storage _ {done: false} run function count_bottles
```
- `count_bottles-0.mcfunction`
```mcfunction
# print "no more bottles"
tellraw @s "no more bottles"

# no more calls
data modify storage _ done set value true

# prevent fall-through
data modify storage _ matched set value true
```
- `count_bottles-1.mcfunction`
```mcfunction
# print "1 bottle"
tellraw @s "1 bottle"

# more calls
data modify storage _ n set value 0
data modify storage _ done set value false

# prevent fall-through
data modify storage _ matched set value true
```
- `count_bottles-2.mcfunction`
```mcfunction
# print "{n} bottles"
tellraw @s [{"nbt": "n", "storage": "_"}, " bottles"]

# more calls
execute store result storage _ n int 0.9999999999999999 run data get storage _ n
data modify storage _ done set value false
```

This transformation is tail-recursive and also uses a constant storage space.
Unlike the general trampolines, our trampoline's continuation is specialized for `count_bottles`.

## Conclusion

We have discovered a way to make a function tail-recursive modulo pattern matching, without the weird function tags technique.
