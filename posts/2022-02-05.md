# List-Mapped Trie: An NBT Data Structure Indexable in Ω(logN)-O(log²N) Time

## Introduction

In this post, we will discuss *list-mapped trie*, which is an NBT data structure indexable by a score or an int tag in `Ω(logN)`-`O(log²N)` time.

## Selectors

First, we will discuss *selector*, which is the building block of list-mapped trie.
A simple on-off selector looks like:

```nim
# storage _
{selector: [["off"], ["on"]]}
```

We can select the value `"off"` as follows:

```mcfunction
## "off"
data get storage _ selector[-2][0]
```

Let's switch the selector to `"on"`:

```mcfunction
data modify storage _ selector append value []
```

The selector now looks like:

```nim
# storage _
{selector: [["off"], ["on"], []]}
```

We can use the same command as before to select the value `"on"` as follows:

```mcfunction
## "on"
data get storage _ selector[-2][0]
```

To switch the selector to `"off"` again, remove the appended `[]`.

```mcfunction
data remove storage _ selector[-1]
```

This is the basic idea of selectors.
By modifying the state of the selector itself, we can select different values for the same NBT path.

## Nested Selectors

Now that we know the basics of selectors, let's consider selectors of selectors.
We can nest selectors as follows:

```nim
# storage _
{selector: [
    [["a"], ["b"]],
    [["c"], ["d"]]
]}
```

We can select the value `"a"` as follows:

```mcfunction
## "a"
data get storage _ selector[-2][-2][0]
```

We can switch the selector to `"d"` as follows:

```mcfunction
data modify storage _ selector append value []
data modify storage _ selector[-2] append value []
```

Notice that the second command makes use of the first selector `selector[-2]` to switch the nested (second) selector.

The selector now looks like:

```nim
# storage _
{selector: [
    [["a"], ["b"]],
    [["c"], ["d"], []],
    []
]}
```

We can use the same command as before to select the value `"d"` as follows:

```mcfunction
## "d"
data get storage _ selector[-2][-2][0]
```

In this way, we can select `N` values by nesting `logN` selectors in general.
List-mapped trie is a simply nested selector thing (and caching).
Indexing can be done by switching the selectors depending on the score.

## Complexity

To switch all the nested selectors towards the target index, we need to switch at most `logN` selectors.
As shown in the previous example, to switch the `k`th selector, the `(k-1)`th selector must be switched first.
This implies that the computational complexity (upper bound) of indexing is as follows:

```
O(sum k, k=1 to logN) = O(log²N)
```

Once all the switching is done, the access only takes `Θ(logN)` time, because we only need to walk through `logN` selectors.
Caching can reduce the computational complexity of successive indexing to the same index to `Θ(logN)`.

Therefore, the computational complexity of indexing a list-mapped trie is `Ω(logN)`-`O(log²N)`.

## Implementation

Here's my implementation of list-mapped trie.

- [Binary shift](https://gist.github.com/intsuc/0901df9d487f7829d97491613a12d351)
- [6-ary shift](https://github.com/intsuc/Heap/tree/main/Heap/data/heap/functions/core/touch_cell/shift)
- [6-ary filter](https://github.com/intsuc/Heap/tree/main/Heap/data/heap/functions/core/touch_cell/internal)

Binary shift is the one presented in this post.
6-ary shift is a minor modification of binary to 6-ary for better performance and footprint.
6-ary filter uses compound tags and can select multiple indices.
