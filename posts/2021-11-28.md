# Mutual Tail Recursion Modulo Pattern Matching

This is a short post on how to make mutual-tail-recursion-modulo-pattern-matching command-queue-safe.
Consider the following mutually tail recursive functions.

```lean
def f
  | 0 => 0
  | 1 => g 0
  | n => h (n - 1)

def g
  | 0 => 1
  | 1 => h 0
  | n => f (n - 1)

def h
  | 0 => 2
  | 1 => f 0
  | n => g (n - 1)
```

If we naively translate the above program into mcfunction, we will get a command-queue-unsafe program.

Fortunately, there is a solution.
In each of the mutually recursive functions, instead of calling the function directly, set the tag of the function to be called.

- `f.mcfunction`
```mcfunction
execute if score argument _ matches 0 run scoreboard players set return _ 0

# g
execute if score argument _ matches 1 run scoreboard players set tag _ 1
# h
execute unless score argument _ matches 0..1 run scoreboard players set tag _ 2
```
- `g.mcfunction`
```mcfunction
execute if score argument _ matches 0 run scoreboard players set return _ 1

# h
execute if score argument _ matches 1 run scoreboard players set tag _ 2
# f
execute unless score argument _ matches 0..1 run scoreboard players set tag _ 0
```
- `h.mcfunction`
```mcfunction
execute if score argument _ matches 0 run scoreboard players set return _ 2

# f
execute if score argument _ matches 1 run scoreboard players set tag _ 0
# g
execute unless score argument _ matches 0..1 run scoreboard players set tag _ 1
```

Then, use the tail recursive function to dispatch these functions.

- `dispatch.mcfunction`
```mcfunction
scoreboard players operation scrutinee _ = tag _
scoreboard players set tag _ -1

execute if score scrutinee _ matches 0 run function f
execute if score scrutinee _ matches 1 run function g
execute if score scrutinee _ matches 2 run function h

# If `tag` is set, re-dispatch.
execute unless score tag _ matches -1 run function dispatch
```

Let's call `f`. We cannot call `f` directly since `f` cannot call other functions. We have to call `dispatch` instead.

```mcfunction
scoreboard players set argument _ 10
# f
scoreboard players set tag _ 0
function dispatch
```

## Conclusion

By using the dispatch loop, we can make mutual-tail-recursion-modulo-pattern-matching command-queue-safe.
