# Visibility of Entities

This post explains the visibility of entities (whether they are accessible from commands or not) in the form of quizzes.

## Preparation

The dedicated server (`server.jar`) of version 1.19.3 is used.

Set `server.properties` as follows:

```properties
function-permission-level=4
max-tick-time=-1
```

`datapacks` contains only the datapacks used by this quiz.
At this point, there are no other files or directories other than these in the directory containing `server.jar`.

Start the server, execute the following two commands and then shutdown the server.

```mcfunction
setworldspawn 1000000 0 0
summon marker 0.0 0.0 0.0 {UUID: [I; 0, 0, 0, 0]}
```

That's it for the preparation.

## Questions

This question asks whether the marker (`0-0-0-0-0`) added during the preparation can be accessed by command.
Each question Q<i>i</i> has an entry point function <code>q<i>i</i></code>.
Executing function <code>q<i>i</i></code> from the console outputs `A` if `0-0-0-0-0` is visible and `B` if not.
Answer which of the following three options the result will be:
1. Only `A` is displayed
2. Only `B` is displayed
3. Neither option 1 nor 2 apply

Assuming the following conditions:
- Functions are executed from the console after the reuslt of the previously executed function is displayed
- No fatal erros occur in the hardware/OS/JVM

### Q1

- `q1.mcfunction`
```mcfunction
say Q1

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B
```

### Q2

- `q2.mcfunction`
```mcfunction
say Q2

forceload add 0 0

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

### Q3

- `q3.mcfunction`
```mcfunction
say Q3

forceload add 0 0
forceload add 48 0

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 48 0
forceload remove 0 0
```

### Q4

- `q4.mcfunction`
```mcfunction
say Q4

forceload add 0 0

schedule function q4-1 1t
```
- `q4-1.mcfunction`
```mcfunction
execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

### Q5

- `q5.mcfunction`
```mcfunction
say Q5

forceload add 0 0

schedule function q5-1 1s
```
- `q5-1.mcfunction`
```mcfunction
execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

### Q6

- `q6.mcfunction`
```mcfunction
say Q6

forceload add 0 0

save-all

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

### Q7

- `q7.mcfunction`
```mcfunction
say Q7

forceload add 0 0

save-all flush

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

### Answers

### Q1

- `q1.mcfunction`
```mcfunction
say Q1

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B
```

#### Answer

2

#### Explanation

For an entity to be visible, at least the chunk containing the entity must be loaded.
The chunk `[0, 0]` has not been loaded, so the entity is not visible.
Therefore, `B` is displayed.

### Q2

- `q2.mcfunction`
```mcfunction
say Q2

forceload add 0 0

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

#### Answer

2

#### Explanation

The chunk `[0, 0]` is synchronously loaded by `forceload add 0 0`.
This updates the chunk status and starts the pending load of the entities.
It is in the `entityManagement` phase that the pending load is processed and the entities are actually loaded.
The entities do not become visible immediately after the chunk is synchronously loaded.
Therefore, `B` is displayed.

### Q3

- `q3.mcfunction`
```mcfunction
say Q3

forceload add 0 0
forceload add 48 0

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 48 0
forceload remove 0 0
```

#### Answer

2

#### Explanation

The ticket level is updated by `forceload add 48 0` to set the chunk level of the chunk `[0, 0]` to 31.
However, just because the chunk level of a chunk is set to a value at which entities can be ticked (below 31) does not mean that the entities in that chunk will be loaded immediately.
Therefore, `B` is displayed for the same reason as in Q2.

### Q4

- `q4.mcfunction`
```mcfunction
say Q4

forceload add 0 0

schedule function q4-1 1t
```
- `q4-1.mcfunction`
```mcfunction
execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

#### Answer

2

#### Explanation

The visibility checking of the entity is delayed by `schedule function q4-1 1t`.
Here, the order of execution in the tick is as follows:
1. Execute the `schedule`d functions
2. The `entityManagement` phase
3. Execute the commands from the console

When the function `q4-1` is executed after `1t` (1 tick), the `entityManagement` phase has not yet been reached.
Therefore, `B` is displayed for the same reason as in Q2.

### Q5

- `q5.mcfunction`
```mcfunction
say Q5

forceload add 0 0

schedule function q5-1 1s
```
- `q5-1.mcfunction`
```mcfunction
execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

#### Answer

3

#### Explanation

Compared to Q4, the delay for the visibility checking has increased from `1t` (1 tick) to `1s` (20 ticks).
Therefore, the `entityManagement` phase is reached 19 times before the function `q5-1` is executed.
However, in the `entityManagement` phase, only entities for which the pending load has already been completed are loaded.

So when is the pending load complete?
Entities are stored in region files (`.mca`) under the world's `entities` directory, so in order to load entities, these files must be loaded from the storage.[^1]
Minecraft uses a thread pool called the IO pool to efficiently perform this process.
Entities that have completed their  pending load under the IO pool are added to a thread-safe queue called the `loadingInbox`.
At the time of the `entityManagement` phase of each tick, the entities in the `loadingInbox` are actually loaded.

This means that when a pending load completes depends on factors such as the input/output performance of the storage (IOPS) and the number of CPU threads.
The entity may or may not be visible after 20 ticks.
Therefore, neither option 1 nor 2 apply.

### Q6

- `q6.mcfunction`
```mcfunction
say Q6

forceload add 0 0

save-all

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

#### Answer

2

#### Explanation

Saving the server in `flush` mode synchronizes the entity data on storage and in memory for each dimension.
At this time, entity loading is performed synchronously for all visible chunks for data synchronization.
In this case, the above process is not performend because it is not in `flush` mode.
Therefore, `B` is displayed for the same reason as in Q2.

### Q7

- `q7.mcfunction`
```mcfunction
say Q7

forceload add 0 0

save-all flush

execute if entity 0-0-0-0-0 run say A
execute unless entity 0-0-0-0-0 run say B

forceload remove 0 0
```

#### Answer

1

#### Explanation

Since the server is saved in `flush` mode, `B` is displayed for the reason given in Q6.

### Conclusion

If you want to load an entity, wait until the entity is loaded, since it is silly and not performant to `save-all flush` every time.

[^1]: Thanks to write-back, it may actually be loaded from the cache in memory.
