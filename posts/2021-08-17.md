# Tail Recursion under Multiple Continuations

## Introduction

In [the previous post](https://github.com/intsuc/blog/blob/main/posts/2021-08-14.md), we found how to make a function tail-recursive modulo pattern matching.
This is achievable because there is only one possible continuation (`count_bottles`), and the only choice is to call it or not.
If we have multiple continuations, we will end up having to use pattern matching to choose one of those continuations.
Such a function is, of course, not tail-recursive.

Fortunately, we have found two ways to resolve this problem.
One is completely impractical, and the other is practical but limited.

## Impractical Method

The first method uses datapacks.
We can do something like dynamic dispatching by sorting a datapack.
Using this idea, we can enable/disable the datapacks by pattern matching to choose a function and call the chosen function at the tail position.

- `main/data/minecraft/functions/main.mcfunction`
```mcfunction
execute if data storage _ {tag: a} run datapack disable "file/b"
execute if data storage _ {tag: a} run datapack enable "file/a"
execute if data storage _ {tag: b} run datapack disable "file/a"
execute if data storage _ {tag: b} run datapack enable "file/b"
function f
```
- `a/data/minecraft/functions/f.mcfunction`
```mcfunction
say a
```
- `b/data/minecraft/functions/f.mcfunction`
```mcfunction
say b
```

This is obviously inefficient and impractical since each time the function is called, the whole datapacks are reloaded at most twice!

## Practical-But-Limited Method

The second method uses advancements.

- `functions/main.mcfunction`
```mcfunction
# prevent the functions from being executed.
data modify storage _ executable set value false

# revoke only the advancement corresponding to `tag`.
advancement grant @s from a
execute if data storage _ {tag: a} run advancement revoke @s only a
execute if data storage _ {tag: b} run advancement revoke @s only b

# allow the function to be executed.
data modify storage _ executable set value true

# execute the function
advancement grant @s from a
```
- `advancements/a.json`
```json
{
    "criteria": {
        "a": {
            "trigger": "minecraft:impossible"
        }
    },
    "rewards": {
        "function": "_a"
    }
}
```
- `advancements/b.json`
```json
{
    "criteria": {
        "b": {
            "trigger": "minecraft:impossible"
        }
    },
    "rewards": {
        "function": "_b"
    },
    "parent": "a"
}
```
- `functions/_a.mcfunction`
```mcfunction
# if and only if `executable`, run `a`.
execute if data storage _ {executable: true} run function a
```
- `functions/_b.mcfunction`
```mcfunction
# if and only if `executable`, run `b`.
execute if data storage _ {executable: true} run function b
```
- `functions/a.mcfunction`
```mcfunction
say a
```
- `functions/b.mcfunction`
```mcfunction
say b
```

This is far better than the previous method but requires at least one player in the world.

## Future Work

To find a more general way to achieve this, without the skeptical `maxCommandChainLength` magic.
